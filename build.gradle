plugins {
  id 'java'
  id 'org.jetbrains.intellij' version '1.9.0'
}

group 'org.intellij.sdk'
version '0.1.3'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots/'
  }
  gradlePluginPortal()
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

test {
  // This path value is machine-specific placeholder text.
  // Set idea.home.path to the absolute path to the intellij-community source
  // on your local machine.
  // Use variants described in https://docs.gradle.org/current/userguide/build_environment.html
  // for real world projects.
  systemProperty "idea.home.path", "/Users/akovari/devel/yona-intellij-plugin"
}

dependencies {
  implementation "org.antlr:antlr4-intellij-adaptor:0.1"
  implementation "com.github.akovari:antlr4-autocomplete:0.0.1-SNAPSHOT"
  implementation "org.yona-lang:parser:0.8.4-SNAPSHOT"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
  version = '2022.2'
  plugins = ['java', 'PsiViewer:222-SNAPSHOT']
}

compileJava   {
  sourceCompatibility '11'
  targetCompatibility '11'
}

buildSearchableOptions {
  enabled = false
}

patchPluginXml {
  version = project.version
  sinceBuild = '201'
  untilBuild = '223.*'
}
